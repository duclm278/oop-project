# Don't use this to avoid bad pages
SELECT DISTINCT * WHERE {
    ?s dct:subject/skos:broader* dbc:Tourist_attractions_in_Vietnam.
    ?s rdfs:label ?label.
    FILTER(LANG(?label) = "en")
}

# Use this to get pages of a category
SELECT DISTINCT * WHERE {
    ?s dct:subject/skos:broader* dbc:Tourist_attractions_in_Vietnam_by_city.
    ?s rdfs:label ?label.
    FILTER(LANG(?label) = "en")
}

# Use this to get uncategorized pages in Tourist_attractions_in_Vietnam
SELECT DISTINCT * WHERE {
    ?s dct:subject dbc:Tourist_attractions_in_Vietnam.
    ?s rdfs:label ?label.
    FILTER(LANG(?label) = "en")
}

# Count number of pages returned
SELECT COUNT(DISTINCT ?s) AS ?count WHERE {
    ?s dct:subject/skos:broader* dbc:Tourist_attractions_in_Vietnam_by_city.
    ?s rdfs:label ?label.
    FILTER(LANG(?label) = "en")
}

# Return everything
SELECT DISTINCT * WHERE {
    ?s dct:subject/skos:broader* dbc:Tourist_attractions_in_Vietnam_by_city.
    ?s ?p ?o.
}

# Return values that:
# 1. Are literals, but don't have a language tag; or
# 2. Are literals having a language tag that matches "en".
SELECT DISTINCT * WHERE {
    ?s dct:subject/skos:broader* dbc:Tourist_attractions_in_Vietnam_by_city.
    ?s ?p ?o.
    FILTER (LANG(?o) = "" || langMATCHES(lang(?o), "en"))
}

# Return values that:
# 1. Are not literals; or
# 2. Are literals, but don't have a language tag; or
# 3. Are literals having a language tag that matches "en".
SELECT DISTINCT * WHERE {
    ?s dct:subject/skos:broader* dbc:Tourist_attractions_in_Vietnam_by_city.
    ?s ?p ?o.
    FILTER (!isLITERAL(?o) || LANG(?o) = "" || langMATCHES(lang(?o), "en"))
}

# Remove bad pages by ?p, ?s and ?o
SELECT DISTINCT * WHERE {
    ?s dct:subject/skos:broader* dbc:Tourist_attractions_in_Vietnam_by_city.
    ?s ?p ?o.
    FILTER (!isLITERAL(?o) || LANG(?o) = "" || langMATCHES(lang(?o), "en"))
    FILTER (STR(?o) != "")
    FILTER (!CONTAINS(LCASE(STR(?p)), "wiki"))
    FILTER (!CONTAINS(LCASE(STR(?s)), "list_of"))
}

# Return a RDF graph of a category
CONSTRUCT {
    ?s ?p ?o.
} WHERE {
    ?s dct:subject/skos:broader* dbc:Tourist_attractions_in_Vietnam_by_city.
    ?s ?p ?o.
    FILTER (!isLITERAL(?o) || LANG(?o) = "" || langMATCHES(lang(?o), "en"))
    FILTER (STR(?o) != "")
    FILTER (!CONTAINS(LCASE(STR(?p)), "wiki"))
    FILTER (!CONTAINS(LCASE(STR(?s)), "list_of"))
}

# Return a RDF graph with ALL categories
CONSTRUCT {
    ?s ?p ?o.
} WHERE {
    {
        VALUES ?category {
            dbc:Tourist_attractions_in_Vietnam_by_city
            dbc:Tourist_attractions_in_Vietnam_by_province
            dbc:Religious_festivals_in_Vietnam
            dbc:Beaches_of_Vietnam
            dbc:Entertainment_venues_in_Vietnam
            dbc:Gardens_in_Vietnam
            dbc:Lakes_of_Vietnam
            dbc:Landmarks_in_Vietnam
            dbc:Museums_in_Vietnam
            dbc:Pagodas_in_Vietnam
            dbc:Parks_in_Vietnam
            dbc:Protected_areas_of_Vietnam
            dbc:Religious_buildings_and_structures_in_Vietnam
            dbc:Show_caves_in_Vietnam
            dbc:Squares_in_Vietnam
            dbc:Waterfalls_of_Vietnam
        }
        ?s dct:subject/skos:broader* ?category.
        ?s ?p ?o.
    } UNION {
        VALUES ?category {
            dbc:Tourist_attractions_in_Vietnam
            dbc:Festivals_in_Vietnam
        }
        ?s dct:subject ?category.
        ?s ?p ?o.
    }
    FILTER (!isLITERAL(?o) || LANG(?o) = "" || langMATCHES(lang(?o), "en"))
    FILTER (STR(?o) != "")
    FILTER (!CONTAINS(LCASE(STR(?p)), "wiki"))
    FILTER (!CONTAINS(LCASE(STR(?s)), "list_of"))
}
